import re
import os
from Autoscons.AutoBuild import Init

def config_script_bld_action(target, source, env):
    def quote(matchobj):
        return "\\"+matchobj.group(0)
    source_fn = str(source[0])
    target_fn = str(target[0])
    prefix = env['PREFIX']
    prefix_quoted = re.sub("[\\\\\"$@]", quote, prefix, 0)
    f = open(source_fn, 'rb')
    source_text = f.read()
    f.close()
    target_text = source_text.replace('<<<PREFIX>>>', prefix_quoted)
    f = open(target_fn, 'wb')
    f.write(target_text)
    f.close()
    os.chmod(target_fn, 0755)

package_files = []

config_script_bld = Builder(action = config_script_bld_action)

env = Environment()
env.Append(BUILDERS = {'ConfigScript': config_script_bld})

config_file = 'latemp.conf'
opts = Options(config_file)
opts.Add(PathOption('PREFIX', 
    'Directory to install under (Default: /usr/local)',
    '/usr/local'))
opts.Update(env)
opts.Save(config_file, env)

idir_prefix = '$PREFIX'
Export('env idir_prefix')

latemp_wml_files = []

themes = [ 
    { 'id':"better-scm" } 
    ]

for file in ['html-header.wml','html-head-tag.wml','h-w-nav-menu.wml', \
        'latemp-main.wml', 'template-params.wml', 'texts.wml', 'utils.wml']:
    latemp_wml_files.append(env.File(file, 'wml/latemp'))
    package_files.append('wml/latemp/' + file)

for t in themes:
    id = t['id']
    t['scons_file'] = env.File('template.wml', 'wml/latemp/themes/' + id + '/')
    package_files.append('wml/latemp/themes/' + id + '/' + 'template.wml')

config_script = env.ConfigScript(target = 'latemp-config', source = 'latemp-config.pl')
package_files.append('latemp-config.pl')
setup_script = env.File('latemp-setup')
package_files.append('latemp-setup')

wml_install_path = idir_prefix + '/lib/wml/include/'
latemp_wml_install_path = wml_install_path + 'latemp/'
for file in latemp_wml_files:
    env.Install(latemp_wml_install_path, file)

for t in themes:
    env.Install(latemp_wml_install_path + 'themes/' + t['id'] + '/', t['scons_file'])

package = Init("latemp", "0.1.4")
package.AddToDist(package_files)
dist_node = package.BuildDist(env)
env.Alias('dist', dist_node)

env.Install(idir_prefix + '/bin', config_script)
env.Install(idir_prefix + '/bin', setup_script)
env.Alias('install', idir_prefix)

